/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.nurserostering.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;

import org.optaplanner.examples.nurserostering.domain.DayOfWeek;
import org.optaplanner.examples.nurserostering.domain.Ta;
import org.optaplanner.examples.nurserostering.domain.CourseAssignment;
import org.optaplanner.examples.nurserostering.domain.NurseRoster;
import org.optaplanner.examples.nurserostering.domain.NurseRosterParametrization;
import org.optaplanner.examples.nurserostering.domain.Course;
import org.optaplanner.examples.nurserostering.domain.CourseDate;
import org.optaplanner.examples.nurserostering.domain.CourseType;
import org.optaplanner.examples.nurserostering.domain.contract.Contract;
import org.optaplanner.examples.nurserostering.domain.contract.BooleanContractLine;
import org.optaplanner.examples.nurserostering.domain.contract.ContractLine;
import org.optaplanner.examples.nurserostering.domain.contract.ContractLineType;
import org.optaplanner.examples.nurserostering.domain.contract.MinMaxContractLine;
import org.optaplanner.examples.nurserostering.domain.request.CourseOffRequest;
import org.optaplanner.examples.nurserostering.domain.request.CourseOnRequest;
import org.optaplanner.examples.nurserostering.solver.drools.TaAssignmentTotal;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

// A ta can't be assigned more than two courses in a single day
rule "noMoreThanTwoCoursesPerDay"
    when
        $firstAssignment : CourseAssignment($firstId : id, $ta : ta, $courseDate : courseDate, ta != null)
        $secondAssignment : CourseAssignment($secondId : id, ta == $ta, courseDate == $courseDate, id != $firstId)
        $thirdAssignment : CourseAssignment($thirdId : id, ta == $ta, courseDate == $courseDate, id != $firstId, id != $secondId)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

// Can't assist with more than one of the same courseType
// on a single day
rule "oneTypePerDay"
    when
        $left : CourseAssignment($id : id, $ta : ta, $courseDate : courseDate, $courseType : course.courseType, ta != null)
        $right : CourseAssignment(ta == $ta, courseDate == $courseDate, course.courseType == $courseType, id > $id)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end


// ############################################################################
// Soft constraints
// ############################################################################

rule "insertTaAssignmentTotal"
        salience 1 // Do these rules first (optional, for performance)
    when
        MinMaxContractLine(contractLineType == ContractLineType.TOTAL_ASSIGNMENTS, enabled == true,
            $contract : contract)
        $ta : Ta(contract == $contract)
        $assignmentTotal : Number() from accumulate(
            $assignment : CourseAssignment(ta == $ta),
            count($assignment)
        )
    then
        insertLogical(new TaAssignmentTotal($ta, $assignmentTotal.intValue()));
end

// A ta should only be assigned to one course per day
rule "oneCoursePerDay"
    when
        $leftAssignment : CourseAssignment($leftId : id, $ta : ta, $courseDate : courseDate, ta != null)
        $rightAssignment : CourseAssignment(ta == $ta, courseDate == $courseDate, id > $leftId)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, -1);
end

// Minimum number of assignments
rule "minimumTotalAssignments"
    when
        $contractLine : MinMaxContractLine(
            contractLineType == ContractLineType.TOTAL_ASSIGNMENTS, minimumEnabled == true,
            $contract : contract, $minimumValue : minimumValue
        )
        TaAssignmentTotal(taContract == $contract, total < $minimumValue,
            $ta : ta, $total : total)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, ($total - $minimumValue) * $contractLine.getMinimumWeight());
end

// Maximum number of assignments
rule "maximumTotalAssignments"
    when
        $contractLine : MinMaxContractLine(
            contractLineType == ContractLineType.TOTAL_ASSIGNMENTS, maximumEnabled == true,
            $contract : contract, $maximumValue : maximumValue
        )
        TaAssignmentTotal(taContract == $contract, total > $maximumValue,
            $ta : ta, $total : total)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, ($maximumValue - $total) * $contractLine.getMaximumWeight());
end

// Requested course on/off
rule "courseOffRequest"
    when
        $courseOffRequest : CourseOffRequest($ta : ta, $course : course, $weight : weight)
        $assignment : CourseAssignment(ta == $ta, course == $course)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight);
end
rule "courseOnRequest"
    when
        $courseOnRequest : CourseOnRequest($ta : ta, $course : course, $weight : weight)
        not CourseAssignment(ta == $ta, course == $course)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight);
end
